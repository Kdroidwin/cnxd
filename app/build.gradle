plugins {
    id 'com.android.application'
}

def packageName = "jp.dip.muracoro.comittona"
def applicationName = "ComittoNxA"
def versionMajor = 1
def versionMinor = 65
def versionPatch = 20
def revision = "A"

android {
    namespace packageName
    compileSdkVersion 33
    ndkVersion "25.1.8937393"
    defaultConfig {
        applicationId packageName
        minSdkVersion 9
        targetSdkVersion 9 // 11以降でSMBまわりでNetworkOnMainThreadExceptionエラー発生
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}${revision}${versionPatch}"
        multiDexEnabled true    // メソッド数が65535を超えてもビルド可能にする
        ndk {
            abiFilters "arm64-v8a", "armeabi-v7a", "x86"
            externalNativeBuild {
                cmake {
                    cppFlags += ''
                }
            }
        }
    }
    applicationVariants.all { variant ->
        if (variant.buildType.name != "release") {
            return
        }
        variant.outputs.all {
            outputFileName = "${applicationName}_${versionName}.apk"
        }
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
        }
    }
    externalNativeBuild {
        cmake {
            path file('src/main/jni/CMakeLists.txt')
            version '3.22.1'
        }
    }
    productFlavors {
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'com.android.support:multidex:1.0.3'    // メソッド数が65535を超えてもビルド可能にする

    // SDカード書き込み用 Android サポートライブラリ
    // 古いバージョンを採用することで、minSdkVersionを最小に抑える
    implementation 'com.android.support:support-compat:24.2.1'
    implementation 'com.android.support:appcompat-v7:24.2.1'
    // SVGファイル(アイコン表示用)
    implementation 'com.caverock:androidsvg-aar:1.4'
    // PDF
    //implementation 'com.joanzapata.pdfview:android-pdfview:1.0.+@aar'
    // 圧縮ファイル
    //implementation 'org.apache.commons:commons-compress:1.20'
    //implementation 'org.tukaani:xz:1.8'
    // 画像
    //implementation 'com.github.bumptech.glide:glide:3.6.1'

    // SMB2接続用ライブラリ
    implementation 'org.codelibs:jcifs:2.1.35'

}
